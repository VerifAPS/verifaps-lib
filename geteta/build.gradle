plugins {
    id("kotlin-convention")
    id("antlr")
}

description = """
    Generalized Test Table
"""

version = "0.10"

def antlrOutputDir = file("${project.buildDir}/generated-src/antlr/main/edu/kit/iti/formal/automation/testtables/grammar")


repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api project(':symbex')
    antlr group: 'org.antlr', name: 'antlr4', version: '4.13.2'
    api 'com.github.jferard:fastods:0.8.1'
    api(libs.clickt)


    api 'com.github.doyaaaaaken:kotlin-csv-jvm:1.10.0'
}

sourceSets {
    main {
        java {
            srcDirs "src/main/java", file("${project.buildDir}/generated-src/antlr/main")
        }
    }
}

generateGrammarSource {
    outputDirectory antlrOutputDir
    arguments << '-package' << 'edu.kit.iti.formal.automation.testtables.grammar'
    arguments << '-visitor'
}


compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource
compileTestKotlin.dependsOn generateTestGrammarSource

task run(type: JavaExec) {
    group = "run"
    classpath = sourceSets.main.runtimeClasspath
    main = 'edu.kit.iti.formal.automation.testtables.apps.Geteta'
    if (project.hasProperty("appArgs")) {
        args Eval.me(project.property("appArgs"))
    }
}

test {
    exclude '**/AutomataDrawerTest.class'
    exclude '**/SmtEncoderTest.class'
    exclude '**/FullStackTest.class'
    exclude "**/SMVModuleBuilderTest.class"
}


